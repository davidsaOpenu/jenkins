pipeline {
    agent any

    triggers {
        gerrit(
            customUrl: '',
            gerritProjects: [[
                branches: [[
                    compareType: 'PLAIN',
                    pattern: "${params.GERRIT_BRANCH}"
                ]],
                compareType: 'PLAIN',
                disableStrictForbiddenFileVerification: false,
                pattern: 'davidsaOpenu/simulator'
            ]],
            serverName: 'review.gerrithub.io',
            triggerOnEvents: [
                patchsetCreated(
                    excludeDrafts: true,
                    excludeTrivialRebase: true,
                    excludeNoCodeChange: true,
                    excludeWip: true
                ),
                draftPublished(),
                changeMerged(),
                changeRestored(),
                commentAddedContains(
                    '(?i)^(Patch Set [0-9]+:)?( [\\w\\\\+-]*)*' +
                    '(\\n\\n)?\\s*(recheck)'
                )
            ]
        )
        cron('@daily')
    }

    options {
        buildDiscarder(
            logRotator(
                daysToKeepStr: '14', 
                numToKeepStr: '24'
            )
        )
        timestamps()
    }

    parameters {
        string(
            name: 'GERRIT_REFSPEC', 
            defaultValue: 'refs/heads/master', 
            description: ''
        )
        string(
            name: 'GERRIT_BRANCH', 
            defaultValue: 'master', 
            description: ''
        )
    }

    stages {
        stage('Acquire lock') {
            steps {
                script {
                    if (env.GERRIT_CHANGE_ID) {
                        // Gerrit builds: job-global + Change-ID specific lock
                        lock(resource: "job-${env.JOB_NAME}") {
                            lock(resource: "gerrit-${env.GERRIT_CHANGE_ID}", inversePrecedence: true) {
			        echo "Acquired locks: job=${env.JOB_NAME}, change=${env.GERRIT_CHANGE_ID}"
                                timeout(time: 400, unit: 'MINUTES') {  // Timeout only during execution
                                    runPipeline()
                                }
                            }
                        }
                    } else {
                        // Non-Gerrit builds: only global lock
                        lock(resource: "job-${env.JOB_NAME}") {
			    echo "Acquired lock: job=${env.JOB_NAME} (non-Gerrit build)"
                            timeout(time: 400, unit: 'MINUTES') {  // Timeout only during execution
                                runPipeline()
                            }
                        }
                    }
                }
            }
        }
    }

    post {
        always {
            script {
                sh '''
                    echo "==================== VM CLEANUP ===================="
                    
                    if [ -f vm_pid.txt ]; then
                        VM_PID=$(cat vm_pid.txt)
                        echo "Cleaning up VM with PID: $VM_PID"
                        kill $VM_PID || true
                        wait $VM_PID 2>/dev/null || true
                        rm -f vm_pid.txt
                    else
                        echo "No VM PID file found - VM may have already been cleaned up"
                    fi

                    if [ -f vm_image.txt ]; then
                        IMAGE_NAME=$(cat vm_image.txt)
                        echo "Removing image file: $IMAGE_NAME"
                        rm -f $IMAGE_NAME
                        rm -f vm_image.txt
                    fi

                    rm -f vm_port.txt

                    echo "VM cleanup completed"
                    echo "==================== END CLEANUP ===================="
                '''
            }
        }
    }
}

def runPipeline() {
    stage('Checkout') {
        catchError(buildResult: 'FAILURE', stageResult: 'FAILURE') {
            script {
                sh "ls; rm -rf *; ls"

                def repos = [
                    [
                        dir: 'simulator',
                        branch: params.GERRIT_BRANCH,
                        url: 'https://review.gerrithub.io/davidsaOpenu/simulator',
                    ],
                    [
                        dir: 'nvme-cli',
                        branch: 'master',
                        url: 'https://review.gerrithub.io/davidsaOpenu/nvme-cli'
                    ],
                    [
                        dir: 'kernel',
                        branch: 'master',
                        url: 'https://review.gerrithub.io/davidsaOpenu/kernel',
                        timeout: 20
                    ],
                    [
                        dir: 'qemu',
                        branch: 'master',
                        url: 'https://review.gerrithub.io/davidsaOpenu/qemu'
                    ],
                    [
                        dir: 'nvmeCompl/tnvme',
                        branch: 'rev-1.2',
                        url: 'https://review.gerrithub.io/davidsaOpenu/tnvme'
                    ],
                    [
                        dir: 'nvmeCompl/dnvme',
                        branch: 'master',
                        url: 'https://review.gerrithub.io/davidsaOpenu/dnvme'
                    ],
                    [
                        dir: 'open-osd',
                        branch: 'master',
                        url: 'https://review.gerrithub.io/davidsaOpenu/open-osd'
                    ],
                ]

                for (repo in repos) {
                    if (repo.dir == 'simulator') {
                        checkout(scmGit(
                            branches: [[name: repo.branch]],
                            extensions: [
                                [$class: 'RelativeTargetDirectory', relativeTargetDir: repo.dir],
                                [$class: 'BuildChooserSetting', buildChooser: [$class: 'GerritTriggerBuildChooser']],
                                [$class: 'SubmoduleOption', recursiveSubmodules: true],
                                [$class: 'CloneOption', timeout: 30],
                            ],
                            userRemoteConfigs: [
			        [refspec: params.GERRIT_REFSPEC, // Specific to simulator
			         url: repo.url]
		            ]
                        ))
                    } else {
                        checkout(scmGit(
                            branches: [[name: repo.branch]],
                            extensions: [
                                [$class: 'RelativeTargetDirectory', relativeTargetDir: repo.dir],
                                [$class: 'CloneOption', timeout: 30],
                                [$class: 'SubmoduleOption',
                                          disableSubmodules: true,
                                          recursiveSubmodules: false],
                            ],
                            userRemoteConfigs: [
			        [url: repo.url] // No refspec for non-simulator
			    ] 
                        ))
                    }
                }
            }
        }
    }

    stage('Handle Depend-On') {
        catchError(buildResult: 'FAILURE', stageResult: 'FAILURE') {
            script {
                dir('simulator/infra/builder') {
                    if (env.GERRIT_CHANGE_NUMBER) {
                        sh "./handle-depend-on-instructions.sh '${WORKSPACE}' ${env.GERRIT_CHANGE_NUMBER} davidsaOpenu gerrit"
                    } else {
                        sh 'echo Periodic run - no need for ./handle-depend-on-instructions.sh'
                    }
                }
            }
        }
    }

    stage('Build and Test') {
        catchError(buildResult: 'FAILURE', stageResult: 'FAILURE') {
            dir('simulator/infra/builder') {
                sh './run-ci.sh'
            }
        }
    }

    stage('Setup VM and Test Podman') {
        catchError(buildResult: 'FAILURE', stageResult: 'FAILURE') {
            dir('simulator/infra/ELK') {
              sh '''
                echo "Testing podman on VM..."
                echo "==================== BOOT PODMAN VM  ===================="
                ./test_on_podman_VM.sh
              '''
            }
        }
    }

    stage('Cleanup') {
        catchError(buildResult: 'FAILURE', stageResult: 'FAILURE') {
            dir('simulator') {
                sh 'echo Cleanup destory vm dont delete the initial ubuntu image'
            }
        }
    }
}
